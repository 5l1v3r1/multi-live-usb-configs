#!ipxe

:selection

menu iPXE boot menu
item debian64		Boot debian 64bit installer
item debian		Boot debian 32bit installer
item debian64gui	Boot debian 64bit installer [gtk]
item debiangui		Boot debian 32bit installer [gtk]
item mageia64		Boot mageia 64bit installer
item mageia		Boot mageia 32bit installer
item opensuse64		Boot opensuse 64bit installer (current version)
item opensuse		Boot opensuse 32bit installer (current version)
item opensuset64	Boot opensuse 64bit installer (tumbleweed)
item opensuset		Boot opensuse 32bit installer (tumbleweed)
item opensuse114	Boot opensuse 11.4 32bit installer
item opensusef64	Boot opensuse Factory 64bit installer
item opensusef32	Boot opensuse Factory 32bit installer
item plop		Boot plop boot manager
item tinycore		Boot tinycore linux
item tinycore64		Boot tinycore linux [64bit kernel only]
item tinycore64pure	Boot tinycore linux [fully 64bit version, pure64]
item dcore-wheezyy	Boot dCore (debian wheezy version)
item dcore-jessie	Boot dCore (debian jessie version)
item arch		Boot archlinux installer
item arch-text		Boot archlinux installer [text mode menu]
item centos		Boot CentOS installer
item scientific		Boot Scientific Linux installer
item fedora		Boot Fedora installer
item fedora-mirror	Boot Fedora installer [mirror]
item ubuntu		Boot Ubuntu installer
item ubuntu64		Boot Ubuntu installer [64bit]
item slitaz		Boot SliTaz
item slitaz-rolling	Boot SliTaz [rolling]


item --key c config	Configure iPXE
item --key x exit	Exit iPXE
set menu-timeout 0



choose system || goto :selection

goto ${system}


:scientific
  dhcp
  set baseurl:string http://ftp.scientificlinux.org/linux/scientific/7/x86_64/os/
  initrd ${baseurl:string}images/pxeboot/initrd.img
  chain ${baseurl:string}images/pxeboot/vmlinuz repo=${baseurl:string}


:slitaz
  dhcp
  set weburl http://mirror.slitaz.org/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot ||
  set weburl http://mirror.switch.ch/ftp/mirror/slitaz/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot ||
  set weburl http://download.tuxfamily.org/slitaz/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot 

:slitaz-rolling
  dhcp
  sanboot http://mirror.slitaz.org/iso/rolling/slitaz-rolling.iso

:fedora


  dhcp
  set 209:string cfg/pxelinux.cfg
  set 210:string http://dl.fedoraproject.org/pub/alt/bfo/
  chain ${210:string}pxelinux.0

:fedora-mirror
# certain fedora mirrors picked by BFO entry (the one above)
# are missing .treeinfo file in the os/ subdir 
# this breaks installation
# if that happens to you, pick a specific mirror that works
# to work that issue around


#
# new layout, replace 'Workstation' with other directory names for fedora variants
#

  dhcp

  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/fedora/linux//releases/21/Server/x86_64/os
  initrd ${baseurl:string}/images/pxeboot/initrd.img
  chain  ${baseurl:string}/images/pxeboot/vmlinuz repo=${baseurl:string}

:centos

  dhcp
  set baseurl:string http://mirror.centos.org/centos/7/os/x86_64/
  initrd ${baseurl:string}isolinux/initrd.img
  chain  ${baseurl:string}isolinux/vmlinuz repo=${baseurl:string}


#
# vivid is currently broken, use syslinux menu with downloaded kernel and initrd instead for now
# changed it anyway, maybe it will get fixed
# https://bugs.launchpad.net/ubuntu/+source/syslinux/+bug/1385069
#


#
# as a workaround, load pxelinux from older ubuntu 
# and then load subsequent files from current release
# it will fail to load menu module, but if you press TAB
# you can get hints on available commands and kick off the install
# usually just typing in 'install' is all you need on that prompt
#

:ubuntu

  dhcp
  set 210:string http://archive.ubuntu.com/ubuntu/dists/vivid/main/installer-i386/current/images/netboot/ubuntu-installer/i386/
#  set 210:string http://archive.ubuntu.com/ubuntu/dists/vivid/main/installer-i386/current/images/netboot/
#  chain ${210:string}pxelinux.0
#  chain http://archive.ubuntu.com/ubuntu/dists/trusty/main/installer-i386/current/images/netboot/pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:ubuntu64

  dhcp
  set 210:string http://archive.ubuntu.com/ubuntu/dists/vivid/main/installer-amd64/current/images/netboot/ubuntu-installer/amd64/
#  set 210:string http://archive.ubuntu.com/ubuntu/dists/vivid/main/installer-amd64/current/images/netboot/
#  chain ${210:string}pxelinux.0
#  chain http://archive.ubuntu.com/ubuntu/dists/trusty/main/installer-amd64/current/images/netboot/pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux


#
# debian, replace links with your favorite mirror
#


:debian64

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/jessie/main/installer-amd64/current/images/netboot/debian-installer/amd64/
  #chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debian

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/jessie/main/installer-i386/current/images/netboot/debian-installer/i386/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debian64gui

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/jessie/main/installer-amd64/current/images/netboot/gtk/debian-installer/amd64/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debiangui

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/jessie/main/installer-i386/current/images/netboot/gtk/debian-installer/i386/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:mageia64
  dhcp
  set baseurl:string http://ftp5.gwdg.de/pub/linux/mageia/distrib/cauldron/x86_64/isolinux/x86_64/

  initrd   ${baseurl:string}all.rdz
  imgfetch ${baseurl:string}vmlinuz
  boot vmlinuz || goto selection


:mageia
  dhcp

  set baseurl:string http://ftp5.gwdg.de/pub/linux/mageia/distrib/cauldron/i586/isolinux/i386/
  initrd   ${baseurl:string}all.rdz
  imgfetch ${baseurl:string}vmlinuz
  boot vmlinuz || goto selection


:opensuse64
  dhcp
#  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/openSUSE-current
  set baseurl:string http://download.opensuse.org/distribution/openSUSE-current
  set arch:string x86_64
  goto opensuse-generic-boot


:opensusef64

  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/factory
  set arch:string x86_64
  goto opensuse-generic-boot


:opensusef32
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/factory
  set arch:string i386
  goto opensuse-generic-boot


:opensuse
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/openSUSE-current
  set arch:string i386
  goto opensuse-generic-boot


:opensuse114
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/11.4
  set arch:string i386
  goto opensuse-generic-boot


:opensuset
  dhcp
  set baseurl:string http://download.opensuse.org/tumbleweed
  set arch:string i386
  goto opensuse-generic-boot

:opensuset64
  dhcp
  set baseurl:string http://download.opensuse.org/tumbleweed
  set arch:string x86_64
  goto opensuse-generic-boot

:opensuse-generic-boot

  initrd   ${baseurl:string}/repo/oss/boot/${arch:string}/loader/initrd
  imgfetch ${baseurl:string}/repo/oss/boot/${arch:string}/loader/linux || goto selection
  boot     linux Textmode=1 install=${baseurl:string}/repo/oss 
#console=ttyAMA0 console=ttyS0 

:plop

  dhcp 
  imgfetch http://some_web_server/memdisk
  initrd http://some_web_server/plpbt.img
  chain memdisk || goto selection

# for some reason direct boot of plop doesn't seem to work correctly
# also, memdisk fails when the *.img file is packed with gzip 
# (which _really_ reduces size)


:tinycore
  dhcp 
  initrd http://repo.tinycorelinux.net/6.x/x86/release/distribution_files/core.gz
  imgfetch http://repo.tinycorelinux.net/6.x/x86/release/distribution_files/vmlinuz
  boot vmlinuz || goto selection

# all other tinycore args still apply here
# it's worth setting up a NFS share for your extensions + persistence in this mode
# to make full use of tinycore's features

# alternately you can load vmlinuz + rootfs.gz + modules.gz , for easier customization
# if you use your custom server


:tinycore64
  dhcp
  initrd http://repo.tinycorelinux.net/6.x/x86/release/distribution_files/core64.gz
  imgfetch http://repo.tinycorelinux.net/6.x/x86/release/distribution_files/vmlinuz64
  boot vmlinuz64 || goto selection


:tinycore64pure
  dhcp
  initrd http://repo.tinycorelinux.net/6.x/x86_64/release/distribution_files/corepure64.gz
  imgfetch http://repo.tinycorelinux.net/6.x/x86_64/release/distribution_files/vmlinuz64
  boot vmlinuz64 || goto selection

:dcore-wheezy

  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-wheezy/dCore-wheezy.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-wheezy/vmlinuz-wheezy
  boot vmlinuz-wheezy || goto selection


:dcore-jessie
  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-jessie/dCore-jessie.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-jessie/vmlinuz-jessie
  boot vmlinuz-jessie || goto selection

:arch

  dhcp

# config file name
  set 209:string cfg/arch.cfg
# path prefix
  set 210:string http://releng.archlinux.org/pxeboot/boot/
  chain ${210:string}pxelinux.0

# other params are documented on http://www.syslinux.org/wiki/index.php/PXELINUX

:arch-text

  dhcp
  set 209:string cfg/arch_text.cfg
  set 210:string http://releng.archlinux.org/pxeboot/boot/
  chain ${210:string}pxelinux.0

